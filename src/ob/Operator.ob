MODULE Operator;
IMPORT 
    Cast, 
    Code,
    ConstValue, 
    Designator,
    Errors, 
    Expression,
    LanguageContext, 
    OberonRtl, 
    Precedence := CodePrecedence,
    String,
    Types;
CONST
    equalCode* = " == ";
    notEqualCode* = " != ";
TYPE
    BinaryProc* = PROCEDURE(left, right: Expression.PType): Expression.PType;

    BinaryOp = PROCEDURE(left, right: ConstValue.PType): ConstValue.PType;
    CodePredicate = PROCEDURE(left, right: STRING; rtl: OberonRtl.PType): STRING;

    UnaryOp = PROCEDURE(value: ConstValue.PType): ConstValue.PType;

    CodeMaker = RECORD
        PROCEDURE make(left, right: STRING; rtl: OberonRtl.PType): STRING
    END;
    PCodeMaker = POINTER TO CodeMaker;

    SimpleCodeMaker = RECORD (CodeMaker)
        PROCEDURE SimpleCodeMaker(code: STRING);

        code: STRING
    END;

    IntCodeMaker = RECORD (SimpleCodeMaker)
    END;

    PredCodeMaker = RECORD (CodeMaker)
        PROCEDURE PredCodeMaker(pred: CodePredicate);

        pred: CodePredicate
    END;

    CastToUint8* = RECORD (Cast.CastOpDoNothing)
    END;
VAR
    openArrayChar: Types.PType;
    castOperations*: Cast.Operations;

PROCEDURE binary(
    left, right: Expression.PType; 
    rtl: OberonRtl.PType;
    op: BinaryOp;
    code: PCodeMaker;
    precedence: INTEGER;
    optResultType: Types.PType;
    optResultPrecedence: INTEGER
    ): Expression.PType;
VAR
    result: Expression.PType;
    leftValue, rightValue, resultValue: ConstValue.PType;
    leftCode, rightCode, resultCode: STRING;
    resultType: Types.PType;
    resultPrecedence: INTEGER;
    rightExpDeref: Expression.PType;
BEGIN
    leftValue := left.constValue();
    rightValue := right.constValue();
    IF (leftValue # NIL) & (rightValue # NIL) THEN
        resultValue := op(leftValue, rightValue);
    END;

    leftCode := Code.adjustPrecedence(Expression.deref(left), precedence);

    (* right code needs parentheses even if it has the same percedence *)
    rightExpDeref := Expression.deref(right);
    IF precedence # Precedence.none THEN
        rightCode := Code.adjustPrecedence(rightExpDeref, precedence - 1);
    ELSE
        rightCode := rightExpDeref.code();
    END;

    resultCode := code.make(leftCode, rightCode, rtl);

    IF optResultType # NIL THEN
        resultType := optResultType;
    ELSE
        resultType := left.type();
    END;

    IF optResultPrecedence # Precedence.none THEN
        resultPrecedence := optResultPrecedence;
    ELSE
        resultPrecedence := precedence;
    END;
    RETURN NEW Expression.Type(resultCode, resultType, NIL, resultValue, resultPrecedence)
END binary;

PROCEDURE SimpleCodeMaker.make(left, right: STRING; rtl: OberonRtl.PType): STRING;
    RETURN left + SELF.code + right
END SimpleCodeMaker.make;

PROCEDURE IntCodeMaker.make(left, right: STRING; rtl: OberonRtl.PType): STRING;
BEGIN
    RETURN SUPER(left, right, rtl) + " | 0"
END IntCodeMaker.make;

PROCEDURE PredCodeMaker.make(left, right: STRING; rtl: OberonRtl.PType): STRING;
BEGIN
    RETURN SELF.pred(left, right, rtl)
END PredCodeMaker.make;

PROCEDURE SimpleCodeMaker.SimpleCodeMaker(code: STRING)
    | code(code);
END;

PROCEDURE PredCodeMaker.PredCodeMaker(pred: CodePredicate)
    | pred(pred);
END;

PROCEDURE binaryWithCodeEx(
    left, right: Expression.PType; 
    op: BinaryOp;
    code: STRING;
    precedence: INTEGER;
    optResultType: Types.PType;
    optResultPrecedence: INTEGER
    ): Expression.PType;
    RETURN binary(
        left, 
        right, 
        NIL, 
        op, 
        NEW SimpleCodeMaker(code), 
        precedence, 
        optResultType, 
        optResultPrecedence)
END;

PROCEDURE binaryWithCode*(
    left, right: Expression.PType; 
    op: BinaryOp;
    code: STRING;
    precedence: INTEGER
    ): Expression.PType;
    RETURN binaryWithCodeEx(left, right, op, code, precedence, NIL, Precedence.none)
END binaryWithCode;

PROCEDURE relational*(
    left, right: Expression.PType; 
    op: BinaryOp;
    code: STRING
    ): Expression.PType;
    RETURN binaryWithCodeEx(left, right, op, code, Precedence.relational, Types.basic.bool, Precedence.none)
END;

PROCEDURE equal*(
    left, right: Expression.PType; 
    op: BinaryOp;
    code: STRING
    ): Expression.PType;
    RETURN binaryWithCodeEx(left, right, op, code, Precedence.equal, Types.basic.bool, Precedence.none)
END;

PROCEDURE promoteToWideIfNeeded(e: Expression.PType): Expression.PType;
VAR
    result: Expression.PType;
BEGIN
    IF e.type() # Types.basic.uint8 THEN
        result := e;
    ELSE
        result := NEW Expression.Type(
                e.code(),
                Types.basic.integer,
                e.designator(),
                e.constValue(),
                e.maxPrecedence());
    END;
    RETURN result
END promoteToWideIfNeeded;

PROCEDURE binaryInt(
    left, right: Expression.PType; 
    op: BinaryOp;
    code: STRING;
    precedence: INTEGER
    ): Expression.PType;
    RETURN promoteToWideIfNeeded(binary(
        left, 
        right, 
        NIL, 
        op, 
        NEW IntCodeMaker(code), 
        precedence, 
        NIL, 
        Precedence.bitOr (* see IntCodeMaker.make *)
        ))
END binaryInt;

PROCEDURE binaryPred(
    left, right: Expression.PType; 
    rtl: OberonRtl.PType;
    op: BinaryOp;
    pred: CodePredicate
    ): Expression.PType;
    RETURN binary(
        left, 
        right, 
        rtl, 
        op, 
        NEW PredCodeMaker(pred), 
        Precedence.none, 
        Types.basic.bool, 
        Precedence.none
        )
END binaryPred;

PROCEDURE unary(e: Expression.PType; op: UnaryOp; code: STRING): Expression.PType;
VAR
    value: ConstValue.PType;
BEGIN
    value := e.constValue();
    IF value # NIL THEN
        value := op(value);
    END;
    resultCode <- code 
                + Code.adjustPrecedence(Expression.deref(e), Precedence.unary);
    RETURN NEW Expression.Type(resultCode, e.type(), NIL, value, Precedence.unary)
END;

PROCEDURE castToStr(e: Expression.PType; cx: LanguageContext.PType): STRING;
VAR
    resultExpression: Expression.PType;
    op: LanguageContext.PCastOp;
    ignored: INTEGER;
BEGIN
    ignored := Cast.implicit(e.type(), openArrayChar, FALSE, castOperations, op);
    IF op # NIL THEN
        resultExpression := op.make(cx, e)
    ELSE
        resultExpression := e;
    END;
    RETURN resultExpression.code()
END castToStr;

PROCEDURE opAddReal(left, right: ConstValue.PType): ConstValue.PType;
    RETURN NEW ConstValue.Real(left^(ConstValue.Real).value 
                             + right^(ConstValue.Real).value)
END opAddReal;

PROCEDURE opAddInt(left, right: ConstValue.PType): ConstValue.PType;
    RETURN NEW ConstValue.Int(left^(ConstValue.Int).value 
                           + right^(ConstValue.Int).value)
END opAddInt;

PROCEDURE opSubReal(left, right: ConstValue.PType): ConstValue.PType;
    RETURN NEW ConstValue.Real(left^(ConstValue.Real).value 
                            - right^(ConstValue.Real).value)
END opSubReal;

PROCEDURE opSubInt(left, right: ConstValue.PType): ConstValue.PType;
    RETURN NEW ConstValue.Int(left^(ConstValue.Int).value 
                            - right^(ConstValue.Int).value)
END opSubInt;

PROCEDURE opMulReal(left, right: ConstValue.PType): ConstValue.PType;
    RETURN NEW ConstValue.Real(left^(ConstValue.Real).value 
                             * right^(ConstValue.Real).value)
END opMulReal;

PROCEDURE opMulInt(left, right: ConstValue.PType): ConstValue.PType;
    RETURN NEW ConstValue.Int(left^(ConstValue.Int).value 
                           * right^(ConstValue.Int).value)
END opMulInt;

PROCEDURE opDivReal(left, right: ConstValue.PType): ConstValue.PType;
    RETURN NEW ConstValue.Real(left^(ConstValue.Real).value 
                             / right^(ConstValue.Real).value)
END opDivReal;

PROCEDURE opDivInt(left, right: ConstValue.PType): ConstValue.PType;
    RETURN NEW ConstValue.Int(left^(ConstValue.Int).value 
                          DIV right^(ConstValue.Int).value)
END opDivInt;

PROCEDURE opMod(left, right: ConstValue.PType): ConstValue.PType;
    RETURN NEW ConstValue.Int(left^(ConstValue.Int).value 
                          MOD right^(ConstValue.Int).value)
END opMod;

PROCEDURE opSetUnion(left, right: ConstValue.PType): ConstValue.PType;
    RETURN NEW ConstValue.Set(left^(ConstValue.Set).value 
                            + right^(ConstValue.Set).value)
END opSetUnion;

PROCEDURE opSetDiff(left, right: ConstValue.PType): ConstValue.PType;
    RETURN NEW ConstValue.Set(left^(ConstValue.Set).value 
                            - right^(ConstValue.Set).value)
END opSetDiff;

PROCEDURE opSetIntersection(left, right: ConstValue.PType): ConstValue.PType;
    RETURN NEW ConstValue.Set(left^(ConstValue.Set).value 
                            * right^(ConstValue.Set).value)
END opSetIntersection;

PROCEDURE opSetSymmetricDiff(left, right: ConstValue.PType): ConstValue.PType;
    RETURN NEW ConstValue.Set(left^(ConstValue.Set).value 
                            / right^(ConstValue.Set).value)
END opSetSymmetricDiff;

PROCEDURE opSetInclL(left, right: ConstValue.PType): ConstValue.PType;
    RETURN NEW ConstValue.Int(ORD(left^(ConstValue.Set).value 
                               <= right^(ConstValue.Set).value))
END opSetInclL;

PROCEDURE opSetInclR(left, right: ConstValue.PType): ConstValue.PType;
    RETURN NEW ConstValue.Int(ORD(left^(ConstValue.Set).value 
                               >= right^(ConstValue.Set).value))
END opSetInclR;

PROCEDURE opOr(left, right: ConstValue.PType): ConstValue.PType;
    RETURN NEW ConstValue.Int(ORD((left^(ConstValue.Int).value # 0)
                               OR (right^(ConstValue.Int).value # 0)))
END opOr;

PROCEDURE opAnd(left, right: ConstValue.PType): ConstValue.PType;
    RETURN NEW ConstValue.Int(ORD((left^(ConstValue.Int).value # 0)
                                & (right^(ConstValue.Int).value # 0)))
END opAnd;

PROCEDURE opEqualInt(left, right: ConstValue.PType): ConstValue.PType;
    RETURN NEW ConstValue.Int(ORD(left^(ConstValue.Int).value
                                = right^(ConstValue.Int).value))
END opEqualInt;

PROCEDURE opEqualReal(left, right: ConstValue.PType): ConstValue.PType;
    RETURN NEW ConstValue.Int(ORD(left^(ConstValue.Real).value
                                = right^(ConstValue.Real).value))
END opEqualReal;

PROCEDURE opEqualSet(left, right: ConstValue.PType): ConstValue.PType;
    RETURN NEW ConstValue.Int(ORD(left^(ConstValue.Set).value
                                = right^(ConstValue.Set).value))
END opEqualSet;

PROCEDURE opNotEqualInt(left, right: ConstValue.PType): ConstValue.PType;
    RETURN NEW ConstValue.Int(ORD(left^(ConstValue.Int).value
                                # right^(ConstValue.Int).value))
END opNotEqualInt;

PROCEDURE opNotEqualReal(left, right: ConstValue.PType): ConstValue.PType;
    RETURN NEW ConstValue.Int(ORD(left^(ConstValue.Real).value
                                # right^(ConstValue.Real).value))
END opNotEqualReal;

PROCEDURE opNotEqualSet(left, right: ConstValue.PType): ConstValue.PType;
    RETURN NEW ConstValue.Int(ORD(left^(ConstValue.Set).value
                                # right^(ConstValue.Set).value))
END opNotEqualSet;

PROCEDURE opLessInt(left, right: ConstValue.PType): ConstValue.PType;
    RETURN NEW ConstValue.Int(ORD(left^(ConstValue.Int).value
                                < right^(ConstValue.Int).value))
END opLessInt;

PROCEDURE opLessReal(left, right: ConstValue.PType): ConstValue.PType;
    RETURN NEW ConstValue.Int(ORD(left^(ConstValue.Real).value
                                < right^(ConstValue.Real).value))
END opLessReal;

PROCEDURE opGreaterInt(left, right: ConstValue.PType): ConstValue.PType;
    RETURN NEW ConstValue.Int(ORD(left^(ConstValue.Int).value
                                > right^(ConstValue.Int).value))
END opGreaterInt;

PROCEDURE opGreaterReal(left, right: ConstValue.PType): ConstValue.PType;
    RETURN NEW ConstValue.Int(ORD(left^(ConstValue.Real).value
                                > right^(ConstValue.Real).value))
END opGreaterReal;

PROCEDURE opEqLessInt(left, right: ConstValue.PType): ConstValue.PType;
    RETURN NEW ConstValue.Int(ORD(left^(ConstValue.Int).value
                               <= right^(ConstValue.Int).value))
END opEqLessInt;

PROCEDURE opEqLessReal(left, right: ConstValue.PType): ConstValue.PType;
    RETURN NEW ConstValue.Int(ORD(left^(ConstValue.Real).value
                               <= right^(ConstValue.Real).value))
END opEqLessReal;

PROCEDURE opEqGreaterInt(left, right: ConstValue.PType): ConstValue.PType;
    RETURN NEW ConstValue.Int(ORD(left^(ConstValue.Int).value
                               >= right^(ConstValue.Int).value))
END opEqGreaterInt;

PROCEDURE opEqGreaterReal(left, right: ConstValue.PType): ConstValue.PType;
    RETURN NEW ConstValue.Int(ORD(left^(ConstValue.Real).value
                               >= right^(ConstValue.Real).value))
END opEqGreaterReal;

PROCEDURE opNot(x: ConstValue.PType): ConstValue.PType;
    RETURN NEW ConstValue.Int(ORD(~(x^(ConstValue.Int).value # 0)))
END opNot;

PROCEDURE opNegateInt(x: ConstValue.PType): ConstValue.PType;
    RETURN NEW ConstValue.Int(-x^(ConstValue.Int).value)
END opNegateInt;

PROCEDURE opNegateReal(x: ConstValue.PType): ConstValue.PType;
    RETURN NEW ConstValue.Real(-x^(ConstValue.Real).value)
END opNegateReal;

PROCEDURE opUnaryPlus(x: ConstValue.PType): ConstValue.PType;
    RETURN x
END opUnaryPlus;

PROCEDURE opSetComplement(x: ConstValue.PType): ConstValue.PType;
    RETURN NEW ConstValue.Set(-x^(ConstValue.Set).value)
END opSetComplement;

PROCEDURE opLsl(left, right: ConstValue.PType): ConstValue.PType;
    RETURN NEW ConstValue.Int(LSL(left^(ConstValue.Int).value,
                                  right^(ConstValue.Int).value))
END opLsl;

PROCEDURE opAsr(left, right: ConstValue.PType): ConstValue.PType;
    RETURN NEW ConstValue.Int(ASR(left^(ConstValue.Int).value,
                                  right^(ConstValue.Int).value))
END opAsr;

PROCEDURE opRor(left, right: ConstValue.PType): ConstValue.PType;
    RETURN NEW ConstValue.Int(ROR(left^(ConstValue.Int).value,
                                  right^(ConstValue.Int).value))
END opRor;

PROCEDURE codeSetInclL(left, right: STRING; rtl: OberonRtl.PType): STRING;
BEGIN
    RETURN rtl.setInclL(left, right)
END codeSetInclL;

PROCEDURE codeSetInclR(left, right: STRING; rtl: OberonRtl.PType): STRING;
    RETURN rtl.setInclR(left, right)
END codeSetInclR;

PROCEDURE strCmp(op: STRING; left, right: Expression.PType; cx: LanguageContext.PType): Expression.PType;
    RETURN Expression.makeSimple(
            cx.rtl.strCmp(castToStr(left, cx), castToStr(right, cx)) + op + "0",
            Types.basic.bool)
END;

PROCEDURE assign*(left, right: Expression.PType; cx: LanguageContext.PType): STRING;
VAR
    designator: Designator.PType;
    leftCode, rightCode: STRING;
    isArray: BOOLEAN;
    castOperation: LanguageContext.PCastOp;
    ignored: BOOLEAN;
    result: STRING;

    PROCEDURE assignArrayFromString(VAR a: Types.Array; s: Types.String): STRING;
    BEGIN
        IF ~(a IS Types.StaticArray) THEN
            Errors.raise("string cannot be assigned to open " + a.description());
        ELSIF Types.stringLen(s) > a.length() THEN
            Errors.raise(String.fromInt(a.length()) 
                         + "-character ARRAY is too small for "
                         + String.fromInt(Types.stringLen(s))
                         + "-character string");
        END;
        RETURN cx.rtl.assignArrayFromString(leftCode, rightCode)
    END assignArrayFromString;
BEGIN
    designator := left.designator();
    info <- designator.info();
    IF ~(info IS Types.PVariable) OR info.isReadOnly() THEN
        Errors.raise("cannot assign to " + info.idType());
    END; 

    leftCode := left.lval();
    rightCode := right.code();
    leftType <- left.type();
    rightType <- right.type();

    isArray := leftType IS Types.PArray;
    IF isArray
        & (Types.arrayElementsType(leftType(Types.PArray)^) = Types.basic.ch)
        & (rightType IS Types.PString) THEN
        result := assignArrayFromString(leftType(Types.PArray)^, rightType^);
    ELSE
        IF cx.types.implicitCast(rightType, leftType, FALSE, castOperation)
            # Cast.errNo THEN
            Errors.raise("type mismatch: '" + left.code() + "' is '" + leftType.description()
                         + "' and cannot be assigned to '" + rightType.description() + "' expression");
        END;
        IF (leftType IS Types.POpenArray) & (rightType IS Types.PArray) THEN
            Errors.raise("'" + leftCode + "' is open '" + leftType.description() + "' and cannot be assigned");
        END;
            
        result := castOperation.assign(cx, left, right);
    END;
    RETURN result
END assign;
    
PROCEDURE inplace(left, right: Expression.PType; cx: LanguageContext.PType; code: STRING; altOp: BinaryProc): STRING;
VAR
    designator: Designator.PType;
    rightExp: Expression.PType;
    result: STRING;
BEGIN
    designator := left.designator();
    info <- designator.info();
    IF (info IS Types.PVariable) & info.isReference() THEN
        result := assign(left, altOp(left, right), cx);
    ELSE
        rightExp := Expression.deref(right);
        result := left.code() + code + rightExp.code();
    END;
    RETURN result
END inplace;

PROCEDURE addReal*(left, right: Expression.PType): Expression.PType;
    RETURN binaryWithCode(left, right, opAddReal, " + ", Precedence.addSub)
END addReal;

PROCEDURE addInt*(left, right: Expression.PType): Expression.PType;
    RETURN binaryInt(left, right, opAddInt, " + ", Precedence.addSub)
END addInt;

PROCEDURE subReal*(left, right: Expression.PType): Expression.PType;
    RETURN binaryWithCode(left, right, opSubReal, " - ", Precedence.addSub)
END subReal;

PROCEDURE subInt*(left, right: Expression.PType): Expression.PType;
    RETURN binaryInt(left, right, opSubInt, " - ", Precedence.addSub)
END subInt;

PROCEDURE mulReal*(left, right: Expression.PType): Expression.PType;
    RETURN binaryWithCode(left, right, opMulReal, " * ", Precedence.mulDivMod)
END mulReal;

PROCEDURE mulInt*(left, right: Expression.PType): Expression.PType;
    RETURN binaryInt(left, right, opMulInt, " * ", Precedence.mulDivMod)
END mulInt;

PROCEDURE divReal*(left, right: Expression.PType): Expression.PType;
    RETURN binaryWithCode(left, right, opDivReal, " / ", Precedence.mulDivMod)
END divReal;

PROCEDURE divInt*(left, right: Expression.PType): Expression.PType;
    RETURN binaryInt(left, right, opDivInt, " / ", Precedence.mulDivMod)
END divInt;

PROCEDURE mod*(left, right: Expression.PType): Expression.PType;
    RETURN binaryWithCode(left, right, opMod, " % ", Precedence.mulDivMod)
END mod;

PROCEDURE setUnion*(left, right: Expression.PType): Expression.PType;
    RETURN binaryWithCode(left, right, opSetUnion, " | ", Precedence.bitOr)
END setUnion;

PROCEDURE setDiff*(left, right: Expression.PType): Expression.PType;
    RETURN binaryWithCode(left, right, opSetDiff, " & ~", Precedence.bitAnd)
END setDiff;

PROCEDURE setIntersection*(left, right: Expression.PType): Expression.PType;
    RETURN binaryWithCode(left, right, opSetIntersection, " & ", Precedence.bitAnd)
END setIntersection;

PROCEDURE setSymmetricDiff*(left, right: Expression.PType): Expression.PType;
    RETURN binaryWithCode(left, right, opSetSymmetricDiff, " ^ ", Precedence.bitXor)
END setSymmetricDiff;

PROCEDURE setHasBit*(left, right: Expression.PType; cx: LanguageContext.PType): Expression.PType;
    RETURN NEW Expression.Type(
            "1 << " + Code.adjustPrecedence(Expression.deref(left), Precedence.shift) 
            + " & " + Code.adjustPrecedence(Expression.deref(right), Precedence.bitAnd),
            Types.basic.bool,
            NIL,
            NIL,
            Precedence.bitAnd)
END;

PROCEDURE setInclL*(left, right: Expression.PType; cx: LanguageContext.PType): Expression.PType;
    RETURN binaryPred(left, right, cx.rtl, opSetInclL, codeSetInclL)
END setInclL;

PROCEDURE setInclR*(left, right: Expression.PType; cx: LanguageContext.PType): Expression.PType;
    RETURN binaryPred(left, right, cx.rtl, opSetInclR, codeSetInclR)
END setInclR;

PROCEDURE or*(left, right: Expression.PType): Expression.PType;
    RETURN binaryWithCode(left, right, opOr, " || ", Precedence.or)
END or;

PROCEDURE and*(left, right: Expression.PType): Expression.PType;
    RETURN binaryWithCode(left, right, opAnd, " && ", Precedence.and)
END and;

PROCEDURE equalInt*(left, right: Expression.PType; cx: LanguageContext.PType): Expression.PType;
    RETURN equal(left, right, opEqualInt, equalCode)
END equalInt;

PROCEDURE equalReal*(left, right: Expression.PType; cx: LanguageContext.PType): Expression.PType;
    RETURN equal(left, right, opEqualReal, equalCode)
END equalReal;

PROCEDURE equalSet*(left, right: Expression.PType; cx: LanguageContext.PType): Expression.PType;
    RETURN equal(left, right, opEqualSet, equalCode)
END equalSet;

PROCEDURE equalStr*(left, right: Expression.PType; cx: LanguageContext.PType): Expression.PType;
    RETURN strCmp(equalCode, left, right, cx)
END equalStr;

PROCEDURE notEqualInt*(left, right: Expression.PType; cx: LanguageContext.PType): Expression.PType;
    RETURN equal(left, right, opNotEqualInt, notEqualCode)
END notEqualInt;

PROCEDURE notEqualReal*(left, right: Expression.PType; cx: LanguageContext.PType): Expression.PType;
    RETURN equal(left, right, opNotEqualReal, notEqualCode)
END notEqualReal;

PROCEDURE notEqualSet*(left, right: Expression.PType; cx: LanguageContext.PType): Expression.PType;
    RETURN equal(left, right, opNotEqualSet, notEqualCode)
END notEqualSet;

PROCEDURE notEqualStr*(left, right: Expression.PType; cx: LanguageContext.PType): Expression.PType;
    RETURN strCmp(notEqualCode, left, right, cx)
END notEqualStr;

PROCEDURE is*(left, right: Expression.PType): Expression.PType;
    RETURN relational(left, right, NIL, " instanceof ")
END is;

PROCEDURE lessInt*(left, right: Expression.PType; cx: LanguageContext.PType): Expression.PType;
    RETURN relational(left, right, opLessInt, " < ")
END lessInt;

PROCEDURE lessReal*(left, right: Expression.PType; cx: LanguageContext.PType): Expression.PType;
    RETURN relational(left, right, opLessReal, " < ")
END lessReal;

PROCEDURE lessStr*(left, right: Expression.PType; cx: LanguageContext.PType): Expression.PType;
    RETURN strCmp(" < ", left, right, cx)
END lessStr;

PROCEDURE greaterInt*(left, right: Expression.PType; cx: LanguageContext.PType): Expression.PType;
    RETURN relational(left, right, opGreaterInt, " > ")
END greaterInt;

PROCEDURE greaterReal*(left, right: Expression.PType; cx: LanguageContext.PType): Expression.PType;
    RETURN relational(left, right, opGreaterReal, " > ")
END greaterReal;

PROCEDURE greaterStr*(left, right: Expression.PType; cx: LanguageContext.PType): Expression.PType;
    RETURN strCmp(" > ", left, right, cx)
END greaterStr;

PROCEDURE eqLessInt*(left, right: Expression.PType; cx: LanguageContext.PType): Expression.PType;
    RETURN relational(left, right, opEqLessInt, " <= ")
END eqLessInt;

PROCEDURE eqLessReal*(left, right: Expression.PType; cx: LanguageContext.PType): Expression.PType;
    RETURN relational(left, right, opEqLessReal, " <= ")
END eqLessReal;

PROCEDURE eqLessStr*(left, right: Expression.PType; cx: LanguageContext.PType): Expression.PType;
    RETURN strCmp(" <= ", left, right, cx)
END eqLessStr;

PROCEDURE eqGreaterInt*(left, right: Expression.PType; cx: LanguageContext.PType): Expression.PType;
    RETURN relational(left, right, opEqGreaterInt, " >= ")
END eqGreaterInt;

PROCEDURE eqGreaterReal*(left, right: Expression.PType; cx: LanguageContext.PType): Expression.PType;
    RETURN relational(left, right, opEqGreaterReal, " >= ")
END eqGreaterReal;

PROCEDURE eqGreaterStr*(left, right: Expression.PType; cx: LanguageContext.PType): Expression.PType;
    RETURN strCmp(" >= ", left, right, cx)
END eqGreaterStr;

PROCEDURE not*(x: Expression.PType): Expression.PType;
    RETURN unary(x, opNot, "!")
END not;

PROCEDURE negateInt*(x: Expression.PType): Expression.PType;
VAR
    result: Expression.PType;
BEGIN
    overflowCheck <- TRUE;
    c <- x.constValue();
    IF c # NIL THEN
        value <- -c^(ConstValue.Int).value;
        result := NEW Expression.Type(String.fromInt(value), Types.basic.integer, NIL, NEW ConstValue.Int(value), Precedence.unary);
    ELSE
        result := promoteToWideIfNeeded(unary(x, opNegateInt, "-"));
        result := NEW Expression.Type(result.code() + " | 0", result.type(), result.designator(), result.constValue(), Precedence.bitOr);
    END;
    RETURN result;
END;

PROCEDURE negateReal*(x: Expression.PType): Expression.PType;
    RETURN promoteToWideIfNeeded(unary(x, opNegateReal, "-"))
END negateReal;

PROCEDURE unaryPlus*(x: Expression.PType): Expression.PType;
    RETURN unary(x, opUnaryPlus, "")
END unaryPlus;

PROCEDURE setComplement*(x: Expression.PType): Expression.PType;
    RETURN unary(x, opSetComplement, "~")
END setComplement;

PROCEDURE lsl*(left, right: Expression.PType): Expression.PType;
    RETURN binaryWithCode(left, right, opLsl, " << ", Precedence.shift)
END lsl;

PROCEDURE asr*(left, right: Expression.PType): Expression.PType;
    RETURN binaryWithCode(left, right, opAsr, " >> ", Precedence.shift)
END asr;

PROCEDURE ror*(left, right: Expression.PType): Expression.PType;
    RETURN binaryWithCode(left, right, opRor, " >>> ", Precedence.shift)
END ror;

PROCEDURE mulInplace*(left, right: Expression.PType; cx: LanguageContext.PType): STRING;
    RETURN inplace(left, right, cx, " *= ", mulReal)
END mulInplace;

PROCEDURE divInplace*(left, right: Expression.PType; cx: LanguageContext.PType): STRING;
    RETURN inplace(left, right, cx, " /= ", divReal)
END divInplace;

PROCEDURE pow2*(e: Expression.PType): Expression.PType;
VAR
    derefExp: Expression.PType;
BEGIN
    derefExp := Expression.deref(e);
    RETURN Expression.makeSimple("Math.pow(2, " + derefExp.code() + ")",
                                 Types.basic.real)
END pow2;

PROCEDURE log2*(e: Expression.PType): Expression.PType;
VAR
    derefExp: Expression.PType;
BEGIN
    derefExp := Expression.deref(e);
    RETURN NEW Expression.Type(
            "(Math.log(" + derefExp.code() + ") / Math.LN2) | 0",
            Types.basic.integer,
            NIL,
            NIL,
            Precedence.bitOr)
END log2;

PROCEDURE opCastToUint8(left, right: ConstValue.PType): ConstValue.PType;
    RETURN NEW ConstValue.Int(left^(ConstValue.Int).value 
                            * right^(ConstValue.Int).value)
END opCastToUint8;

PROCEDURE CastToUint8.make(cx: LanguageContext.PType; e: Expression.PType): Expression.PType;
    RETURN binaryWithCode(
        e, 
        Expression.make("0xFF", 
                        Types.basic.integer, 
                        NIL, 
                        NEW ConstValue.Int(0FFH)), 
        opCastToUint8, 
        " & ", 
        Precedence.bitAnd)
END;

PROCEDURE CastToUint8.clone(cx: LanguageContext.PType; e: Expression.PType): STRING;
    RETURN SELF.make(cx, e).code();
END;

BEGIN
    openArrayChar := NEW Types.OpenArray(Types.basic.ch);
    castOperations.castToUint8 := NEW CastToUint8();
    castOperations.castToRecord := NEW Cast.CastOpRecord();
END Operator.